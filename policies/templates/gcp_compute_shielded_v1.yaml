# Copyright 2019 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
apiVersion: templates.gatekeeper.sh/v1alpha1
kind: ConstraintTemplate
metadata:
  name: gcp-compute-shielded-v1
spec:
  crd:
    spec:
      names:
        kind: GCPComputeShieldedConstraintV1
      validation:
        openAPIV3Schema:
          properties:
            mode:
              type: string
              enum: [blacklist, whitelist]
              description: "If whitelist, only these instances will allowed to be unshielded. If blacklist, all instances will allowed to be unshielded except the specified ones."
            match_mode:
              type: string
              enum: [exact, regex]
              description: "Whether to use exact string matching or regular expressions against instance names"
            instances:
              type: array
              items: 
                type: string
              description: "The list of instances in your whitelist or blacklist"
  targets:
   validation.gcp.forsetisecurity.org:
      rego: | #INLINE("validator/compute_shielded.rego")
            #
            # Copyright 2018 Google LLC
            #
            # Licensed under the Apache License, Version 2.0 (the "License");
            # you may not use this file except in compliance with the License.
            # You may obtain a copy of the License at
            #
            #      http://www.apache.org/licenses/LICENSE-2.0
            #
            # Unless required by applicable law or agreed to in writing, software
            # distributed under the License is distributed on an "AS IS" BASIS,
            # WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
            # See the License for the specific language governing permissions and
            # limitations under the License.
            #
            
            package templates.gcp.GCPComputeShieldedConstraintV1
            
            import data.validator.gcp.lib as lib
            
            ###########################
            # Find Whitelist/Blacklist Violations
            ###########################
            deny[{
            	"msg": message,
            	"details": metadata,
            }] {
            	constraint := input.constraint
            	lib.get_constraint_params(constraint, params)
            	asset := input.asset
            	asset.asset_type == "compute.googleapis.com/Instance"
            
            	instance := asset.resource.data
            	shielded_config := lib.get_default(instance, "shieldedInstanceConfig", {})
            	lib.get_default(shielded_config, "enableIntegrityMonitoring", false) != true
            	lib.get_default(shielded_config, "enableVtpm", false) != true
            
            	# Check if instance is in blacklist/whitelist
            	match_mode := lib.get_default(params, "match_mode", "exact")
            	mode := lib.get_default(params, "mode", "whitelist")
            	target_instances := lib.get_default(params, "instances", [])
            	trace(sprintf("asset name:%v, target_instances: %v, mode: %v, match_mode: %v", [asset.name, target_instances, mode, match_mode]))
            	instance_name_targeted(asset.name, target_instances, mode, match_mode)
            	message := sprintf("%v is not shielded.", [asset.name])
            	ancestry_path = lib.get_default(asset, "ancestry_path", "")
            
            	metadata := {"ancestry_path": ancestry_path}
            }
            
            ###########################
            # Rule Utilities
            ###########################
            instance_name_targeted(asset_name, instance_filters, mode, match_mode) {
            	mode == "whitelist"
            	match_mode == "exact"
            	matches := {asset_name} & cast_set(instance_filters)
            	count(matches) == 0
            }
            
            instance_name_targeted(asset_name, instance_filters, mode, match_mode) {
            	mode == "blacklist"
            	match_mode == "exact"
            	matches := {asset_name} & cast_set(instance_filters)
            	count(matches) > 0
            }
            
            instance_name_targeted(asset_name, instance_filters, mode, match_mode) {
            	mode == "whitelist"
            	match_mode == "regex"
            	not re_match_name(asset_name, instance_filters)
            }
            
            instance_name_targeted(asset_name, instance_filters, mode, match_mode) {
            	mode == "blacklist"
            	match_mode == "regex"
            	re_match_name(asset_name, instance_filters)
            }
            
            re_match_name(name, filters) {
            	re_match(filters[_], name)
            }
            #ENDINLINE
